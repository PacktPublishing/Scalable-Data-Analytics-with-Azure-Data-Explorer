// demonstrates the use of series_moving_avg_fl(), which makes the code more
// readable
let startTime = toscalar(demo_make_series1 | summarize min(TimeStamp));
let endTime = toscalar(demo_make_series1 | summarize max(TimeStamp));
let binSize = 1h;
let windowSize = 10;
demo_make_series1
| where TimeStamp between (startTime .. endTime)
| where Country == "United States"
| make-series requests=count() default=0 on TimeStamp in range (startTime, endTime, binSize) by Country
| extend movingAverage = series_moving_avg_fl(requests, windowSize, true)
| render timechart  
